000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID.    IMB9072.
000030*
000040 COPY IMCINFRN.
000050*
000060******************************************************************
000070*   GROUP LIST BILL - RESET/REBILL PROGRAM                       *
000080*                                 .                              *
000090*   BILLING FILE LAST BILL REBILL BASED ON PRDRB TRIGGER RECORD  *
000100*   REQUESTS RECEIVED FROM ONLINE BILLING IMO2980.               *
000110*   PROGRAM WILL:                                                *
000120*    1. DELETE EXISTING LBH/LBRS FOR REBILL MONTH                *
000130*    2. RESET PRIOR MTH LBR'S LBR-200 TO N                       *
000140*    3. UPDATE DEDUCTION HISTORY ON EMD RECORD                   *
000150*    4. RESET BILLING FILE BILLING DATES (NEXT,LAST,PREV,2ND PREV*
000160*    5. RESET POLICY BILL-TO DATE .                              *
000170*    6. SCHEDULE BILLING FOR REBILL DATE USING DUMMY GBM         *
000180*    7. RESET GBM-GROUP-REBILL-IND = 'N'                         *
000190*    8. DELETE PRBRB TRIGGER RECORD                              *
000200*                                                               .*
000210******************************************************************
000220******************************************************************
000230*             P R O G R A M   C H A N G E   L O G                *
000240******************************************************************
000250*                                                                *
000260*   LOG #    DATE      NAME         DESCRIPTION                  *
000270*   ------ --------  --------    ------------------------------- *
000280*                                                                *
000290*   W03406 09/11/12  KUHN        Correct LBR read logic.SR12886  *
000300*                                                                *
W03316*   W03316 05/10/12  Mpatil      SR8846 - Termed Policy missing  *
W03316*                                from the bill.                  *
W03316*                                                                *
W03267*   W03267 03/07/12  BOB BELL    IF EMD MISSING, PUT ON REPORT,  *
W03267*                                CONTINUE PROCESSING.            *
W03267*                                                                *
W02391*   W02391 10/16/09  Snader      Don't try to update employee    *
W02391*                                  deduction history for trad    *
W02391*                                  list bill                     *
000400*                                                                *
W02177*   W02177 04/20/09  Lafferty    Fix for related Tracker #7279   *
W02177*                                (see below).  Set Rebill ind to *
W02177*                                'Y' as a default.  Bypass MOVEs *
W02177*                                in '3000-'.                     *
W02154*                                                                *
W02154*   W02154 03/21/09  Lafferty    Capture LBH-220 for Rebill      *
W02154*                                (Tracker #7008)                 *
W02154*                                Also, reset GBM-239 if process  *
W02154*                                error on Rebill                 *
W02154*                                (Tracker #7279)                 *
000510*                                                                *
000520*   W01968 02/13/08  Joe Snader  Reset List Bill Adjustments     *
000530*                                                                *
000540*   W01761 08/23/05  BOB BELL    READ NEXT PRD RB IF CURRENT IS X*
000550*                                                                *
000560*   E00619 07/25/05  BOB BELL    NEW PGM.                        *
000570*                                                                *
000580******************************************************************
000590
000600 ENVIRONMENT DIVISION.
000610 INPUT-OUTPUT SECTION.
000620 FILE-CONTROL.
000630
000640     COPY IMCSOPRT.
000650
000660*****************************************************************
000670*****************************************************************
000680*****   ENVIRONMENT DIVISION INCLUDE IN COPYBOOK BELOW
000690*****************************************************************
000700*****************************************************************
000710*COPY IMCE9072.
000720
000730 DATA DIVISION.
000740 FILE SECTION.
000750     COPY IMCFOPRT.
000760
000770***************************************************************
000780 WORKING-STORAGE SECTION.
000790***************************************************************
000800
000810 01 WORK-AREA-START.
000820    05 FILLER                     PIC X(15)     VALUE
000830       '****** PROGRAM '.
000840    05 WS-DEBUG-PROGRAM           PIC X(08)     VALUE 'IMB9072 '.
000850    05 FILLER                     PIC X(40)     VALUE
000860       'WORKING STORAGE STARTS HERE ****'.
000870    05 WS-REGISTER-REPORT         PIC X(05)     VALUE 'RD10F'.
000880
E00619 01  WS-RUN-CONTROL.
E00578     05  WS-PARM-TYPE                  PIC X(01).
E00578         88  WS-NORMAL                  VALUE 'N'.
E00578         88  WS-CONVERSION              VALUE 'C'.
E00578
000940 01 WS-REPORT-TITLE-AREA.
000950    05 WS-REPORT-TITLE            PIC X(50)     VALUE
000960       'GROUP SALARY DEDUCTION - REBILL LAST BILL REPORT  '.
000970
000980    05 WS-REPORT-TITLE1           PIC X(50)     VALUE
000990       '                                                  '.
001000
001010 01 WS-EMPTY-FILE-LINE.
001020    05 FILLER                     PIC X(41)     VALUE SPACES.
001030    05 FILLER                     PIC X(50)     VALUE
001040       'NO REBILL LAST BILL REQUESTS FOR THIS CYCLE       '.
001050    05 FILLER                     PIC X(41)     VALUE SPACES.
001060
001070 01 WS-NO-ERRORS-LINE.
001080    05 FILLER                     PIC X(41)     VALUE SPACES.
001090    05 FILLER                     PIC X(50)     VALUE
001100       'NO ERRORS IN REBILL REQUEST PROCESSING            '.
001110    05 FILLER                     PIC X(41)     VALUE SPACES.
001120
001130 01 WS-END-OF-REPORT.
001140    05 FILLER                     PIC X(41)     VALUE SPACES.
001150    05 FILLER                     PIC X(50)     VALUE
001160       '* * *   E N D   O F   R E P O R T   * * *         '.
001170    05 FILLER                     PIC X(41)     VALUE SPACES.
001180
001190 01  IO-AREA                           PIC X(4096).
001200
001210 COPY IMCDEDIT.
001220
001230 01  WS-SWITCHES-AREA.
001240     05  WS-END-OF-FILE-SW             PIC X(01) VALUE 'N'.
001250         88  WS-EOF-XRF                          VALUE 'Y'.
001260     05  WS-FIRST-TIME                 PIC X(01) VALUE 'N'.
001270     05  WS-PROCESS-ERROR-IND          PIC X(01) VALUE 'N'.
001280         88  WS-PROCESS-ERROR                    VALUE 'Y'.
E00619     05  WS-FORCE-GBM-DATE-ADJUST-IND  PIC X(01) VALUE 'N'.
E00619         88  WS-FORCE-GBM-DATE-ADJUST            VALUE 'Y'.
E00619     05  WS-ERROR-IND                  PIC X(01) VALUE 'N'.
E00619         88  WS-NO-ERRORS                        VALUE 'N'.
E00619         88  WS-ERRORS                           VALUE 'Y'.
W02177     05  WS-GBM-239-IND                PIC X(01) VALUE 'Y'.
W02177         88  WS-GBM-239-BYPASS-REBILL            VALUE 'N'.
W02177         88  WS-GBM-239-DO-REBILL                VALUE 'Y'.
001370
001380 01 WS-SUBSCRIPTS-AREA.
001390    05 SUB                        PIC 9(05)     VALUE ZEROS.
001400    05 SUB1                       PIC 9(05)     VALUE ZEROS.
001410
E00619 01  WS-HOLD-AREA.
E00619     05  WS-HOLD-LBR-FROM-DELETE  PIC X(500).
E00619     05  WS-CO-FOR-POL-READ       PIC X(04) VALUE SPACES.
E00619     05  WS-POL-FOR-POL-READ      PIC X(10) VALUE SPACES.
W02154     05  WS-HOLD-LBH-220-TOT-PD   PIC 9(09)V99 VALUE ZEROS.
W03316     05  WS-HOLD-RECON-KEY        PIC X(15) VALUE SPACES.
W03406     05  WS-HOLD-CO-FOR-POL-READ  PIC X(04) VALUE SPACES.
W03406     05  WS-HOLD-POL-FOR-POL-READ PIC X(10) VALUE SPACES.
001500
001520     05  WS-PRDRB-RECS            PIC 9(07) VALUE ZERO.
001530     05  WS-GBMS-REBILLED         PIC 9(07) VALUE ZERO.
001510 01  WS-COUNTERS-AREA.
001540
001550 01  RPT-DTL-HEADER-1.
001560     05  FILLER                        PIC  X(50)  VALUE
001570         ' BILLING FILE ID/DAY     CLIENT                   '.
001580     05  FILLER                        PIC  X(50)  VALUE
001590         '     REBILLED DATE  REQUESTED ON     MESSAGES     '.
001600     05  FILLER                        PIC  X(32)  VALUE
001610         '                                '.
001620 01  DETAIL-LINE.
001630     05  FILLER                        PIC  X(03)  VALUE SPACES.
001640     05  DTL-FILE-ID                   PIC  X(10)  VALUE SPACES.
001650     05  FILLER                        PIC  X(03)  VALUE SPACES.
001660     05  DTL-PAYMENT-DAY               PIC  9(02)  VALUE ZEROS.
001670     05  FILLER                        PIC  X(04)  VALUE SPACES.
E00619     05  DTL-CLIENT-NAME               PIC  X(30)  VALUE SPACES.
001690     05  FILLER                        PIC  X(04)  VALUE SPACES.
001700     05  DTL-REBILLED-DATE             PIC  X(10)  VALUE ZEROS.
001710     05  FILLER                        PIC  X(05)  VALUE SPACES.
001720     05  DTL-REQUEST-DATE              PIC  X(10)  VALUE ZEROS.
001730     05  FILLER                        PIC  X(06)  VALUE SPACES.
001740     05  DTL-MESSAGES                  PIC  X(45)  VALUE SPACES.
001750
001760******************************************************************
001770*******  DATABASE RECORD LAYOUTS                           *******
001780******************************************************************
001790 COPY IMCCCRDA.
001800 COPY IMCSCRDA.
001810 COPY IMCPRDDA.
001820 COPY IMCPRDRB.
E00619 COPY IMCPERDA.
001840 COPY IMCEMDDA.
001850 COPY IMCGBMDA.
001860 COPY IMCLBRDA.
001870 COPY IMCLBHDA.
001880 COPY IMCPOLDA.
E00619 COPY IMCINGDA.
W01968 COPY IMCEADDA.
001910
001920******************************************************************
001930*******  COMMON WORKING STORAGE LAYOUTS                    *******
001940******************************************************************
001950 COPY IMCBDBIO.
001960 COPY IMCDJGCP.
001970 COPY IMCDOPRT.
001980 COPY IMCDSTDH.
001990 COPY IMCDTBL.
002000    EJECT
002010*****************************************************************
002020*****************************************************************
002030*****   PROCEDURE DIVISION INCLUDE IN COPYBOOK BELOW
002040*****************************************************************
002050*****************************************************************
002060 COPY IMCL9072.
002070
002080******************************************************************
002090 0000-PROGRAM-START SECTION.
002100******************************************************************
002110
002120     PERFORM 0100-INITIALIZATION
002130
002140
002150     PERFORM 1000-PROCESS-REBILL-REQUESTS
002160
002170     PERFORM 0999-CLOSE-PROGRAM
002180     GOBACK
002190     .
002200
002210 SECTION-EXIT.
002220     EJECT
002230
002240******************************************************************
002250 0100-INITIALIZATION   SECTION.
002260******************************************************************
002270
002280     DISPLAY ' '
002290     DISPLAY '*********************'
002300     DISPLAY '*   I M B 9 0 7 2   *'
002310     DISPLAY '*********************'
002320     DISPLAY ' '
002330***  DISPLAY 'PARM='  WS-PARM-OPTIONAL
002340
E00619***        LINKAGE PARM INDICATOR NOT USED - USING THE
E00619***        SCR-199.
E00619***  IF  WS-PARM-TYPE = 'N' OR 'C'
E00619***    NEXT SENTENCE
E00619***  ELSE
E00619***    MOVE 'N'  TO WS-PARM-TYPE
E00619***  .
E00619
002430     OPEN OUTPUT O-PRT-PRINT-FILE
002440
002450     MOVE 'U'                      TO IO-ACCESS-TYPE
002460                                      WS-ACCESS-TYPE
002470
002480     PERFORM STANDARD-BATCH-STARTUP
002490
E00619     IF   W-SCR-199-RUNNING-CONVERSION = 'Y'
E00619       MOVE 'C' TO WS-PARM-TYPE
E00619     ELSE
E00619       MOVE 'N' TO WS-PARM-TYPE
E00619     .
E00619
E00619     IF  WS-NORMAL
E00619       DISPLAY '********************************'
E00619       DISPLAY '********************************'
E00619       DISPLAY '  ==>> NORMAL PROCESSING  <<==  '
E00619       DISPLAY '********************************'
E00619       DISPLAY '********************************'
KAN          MOVE    '             REBILL LAST LISTBILL              '
KAN            TO    WS-REPORT-TITLE1
E00619     ELSE
E00619       DISPLAY '************************************'
E00619       DISPLAY '************************************'
E00619       DISPLAY '  ==>> CONVERSION PROCESSING  <<==  '
E00619       DISPLAY '************************************'
E00619       DISPLAY '************************************'
E00619       MOVE    '====> C O N V E R S I O N   P R O C E S S <===='
E00619         TO    WS-REPORT-TITLE1
E00619     .
E00619
002740     MOVE WS-DEBUG-PROGRAM         TO WS-STD-HDG1-PROGRAM
002750     MOVE WS-REGISTER-REPORT       TO WS-STD-HDG2-RPT-SFX
002760     MOVE WS-REPORT-TITLE          TO WS-STD-HDG2-RPT-TITLE
002770     MOVE WS-REPORT-TITLE1         TO WS-STD-HDG3-RPT-TITLE
002780     MOVE WS-STANDARD-HEADING-1    TO O-PRT-HEADING-LINE-1
002790     MOVE WS-STANDARD-HEADING-2    TO O-PRT-HEADING-LINE-2
002800     MOVE WS-STANDARD-HEADING-3    TO O-PRT-HEADING-LINE-3
002810     MOVE SPACES                   TO O-PRT-HEADING-LINE-4
002820     MOVE 'N'                      TO IO-TRACE-IND
002830     MOVE 99                       TO O-PRT-LINE-COUNTER
002840     MOVE 4                        TO O-PRT-TITLE-COUNTER
002850     MOVE RPT-DTL-HEADER-1         TO O-PRT-TITLE-LINE-2
002860     MOVE SPACES                   TO O-PRT-TITLE-LINE-1
002870                                      O-PRT-TITLE-LINE-3
002880                                      O-PRT-TITLE-LINE-4
002890
002900     MOVE 'N' TO WS-END-OF-FILE-SW
002910
002920
002930     .
002940
002950 SECTION-EXIT.
002960     EJECT
002970
002980******************************************************************
002990 0999-CLOSE-PROGRAM            SECTION.
003000******************************************************************
003010
003020     DISPLAY ' '
003030     DISPLAY '**************************************'
003040     DISPLAY 'INPUT RB  RECORDS        : ' WS-PRDRB-RECS
003050     DISPLAY 'BILLING FILE IDS REBILLED: ' WS-GBMS-REBILLED
003060     DISPLAY '**************************************'
003070     DISPLAY ' '
003080
KAN        MOVE SPACES TO O-PRT-DETAIL-LINE.
003100
003110     IF  WS-PRDRB-RECS = ZERO
003120       MOVE WS-EMPTY-FILE-LINE     TO O-PRT-DETAIL-LINE
003130       MOVE SPACES                 TO WS-STD-HDG3-RPT-TITLE
003140       MOVE WS-STANDARD-HEADING-3  TO O-PRT-HEADING-LINE-3
003150       PERFORM O-PRT-WRITE-AFTER-3
KAN        ELSE
E00619       STRING '** BILLING FILE REBILL REQUESTS : '
E00619            WS-PRDRB-RECS
E00619            '   BILLING FILE IDs REBILLED : '
E00619            WS-GBMS-REBILLED
E00619       DELIMITED BY SIZE
E00619       INTO O-PRT-DETAIL-LINE
003230       PERFORM O-PRT-WRITE-AFTER-2
E00619     END-IF.

003260     MOVE WS-END-OF-REPORT     TO O-PRT-DETAIL-LINE
003270     PERFORM O-PRT-WRITE-AFTER-3
E00619
003290     CLOSE O-PRT-PRINT-FILE
003300
003310     IF WS-PRDRB-RECS NOT >        ZERO
003320         DISPLAY ' '
003330         DISPLAY 'NO SALARY DED REBILL RECORDS ON PRD FILE '
003340         DISPLAY ' '
003350     .
003360
003370
003380     PERFORM STANDARD-BATCH-SHUTDOWN.
003390
003400 SECTION-EXIT.
003410     EJECT
003420
003430******************************************************************
003440 1000-PROCESS-REBILL-REQUESTS     SECTION.
003450******************************************************************
003460
003470     MOVE 'N'      TO IO-TRACE-IND.
003480
003490 1010-START-BROWSE-PRDRB.
003500
003510     MOVE 'PR'                         TO W-PRD-052-RECORD-ID.
003520     MOVE 'RB'                         TO W-PRD-055-PRODUCT-CHAR
003530     MOVE SPACES                       TO W-PRD-056-SEQUENCE-KEY
003540     MOVE W-PRD-050-PRODUCT-MASTER-KEY TO IO-KEY.
003550     MOVE IO-START-BROWSE-GTEQ         TO IO-FUNC.
003560     PERFORM IO-DATA-BASE.
003570
003580     IF NOT IO-NORMAL
003590       DISPLAY '1010- BAD STBR ON PRODUCT, KEY= '
003600               W-PRD-050-PRODUCT-MASTER-KEY
003610       PERFORM STANDARD-BATCH-ABEND
003620     .
003630
003640 1020-READ-PRDRB-RECORDS.
003650
E00619     SET  WS-NO-ERRORS  TO  TRUE
E00619
003680     MOVE W-PRD-050-PRODUCT-MASTER-KEY TO IO-KEY.
003690     MOVE IO-READ-NEXT                 TO IO-FUNC.
003700     PERFORM IO-DATA-BASE.
003710
003720     IF NOT IO-NORMAL
003730       DISPLAY '1020- BAD READNEXT ON PRODUCT, KEY= '
003740               W-PRD-050-PRODUCT-MASTER-KEY
003750       PERFORM STANDARD-BATCH-ABEND
003760     .
003770
003780     MOVE IO-AREA                 TO W-PRD-PRODUCT-MASTER-DATA.
003790     MOVE W-PRD-056-SEQUENCE-KEY  TO RB-PRODUCT-KEY.
003800     MOVE W-PRD-100-VARIABLE-DATA TO RB-PRODUCT-VARIABLE.
003810
E00619     IF  W-PRD-060-STATUS = 'X'
W01761***    GO TO  SECTION-EXIT
W01761       GO TO  1020-READ-PRDRB-RECORDS
E00619     .
003860
003870     IF W-PRD-054-PRODUCT-NO NOT EQUAL 'RB'
003880        GO TO SECTION-EXIT
003890     .
003900
E00619     ADD  1  TO  WS-PRDRB-RECS
E00619
003930     MOVE RB-PRD-BILLING-FILE-ID  TO DTL-FILE-ID
003940     MOVE RB-KEY-BILLING-DAY      TO DTL-PAYMENT-DAY
003950     MOVE RB-KEY-REBILL-DATE      TO DTL-REBILLED-DATE
003960     MOVE RB-PRD-REQUEST-DATE     TO DTL-REQUEST-DATE
003970
E00619     PERFORM 7000-READ-GBM-PER

E00619     IF  RB-PRD-REBILL-CONV-IND = 'Y'
E00619     AND WS-CONVERSION
E00619       DISPLAY '===> GBM ' RB-PRD-BILLING-FILE-ID
E00619       DISPLAY '===> PRDRB NOT CONVERSION REQUEST - BYPASSED'
E00619       DISPLAY '===>'
004050       STRING  'REBILL FOR ' RB-PRD-BILLING-FILE-ID
E00619               '- NOT CONV REQUEST.  '
004070         DELIMITED BY SIZE
004080         INTO  DTL-MESSAGES
004090       PERFORM 8000-ERROR-PRINT
E00619       GO TO SECTION-EXIT
E00619     .
E00619
E00619*           PER TIM  8/22/05
E00619     IF  RB-KEY-REBILL-DATE < W-GBM-280-GROUP-EFFEC-DATE
E00619       DISPLAY '===> GBM ' RB-PRD-BILLING-FILE-ID
E00619       DISPLAY '===> REBILL DTE LESS THAN GBM EFF - BYPASSED'
E00619       DISPLAY '===>'
004180       STRING  'REBILL DTE FOR ' RB-PRD-BILLING-FILE-ID
E00619               ' < GBM EFF, NO REBILL'
004200         DELIMITED BY SIZE
004210         INTO  DTL-MESSAGES
004220       PERFORM 8000-ERROR-PRINT
E00619       GO TO  1090-MARK-PRDRB
E00619     .
E00619
004260     PERFORM 1100-RESET-LAST-BILLING.
004270
E00619     IF  WS-PROCESS-ERROR
E00619       NEXT SENTENCE
E00619     ELSE
004310       MOVE DETAIL-LINE         TO O-PRT-DETAIL-LINE
E00619       MOVE SPACES  TO  DTL-MESSAGES
004330       PERFORM O-PRT-WRITE-AFTER-1
004340     .
E00619
E00619 1090-MARK-PRDRB.
E00619
E00619     PERFORM 1200-MARK-PRDRB-AS-DONE
E00619
004400     GO TO 1020-READ-PRDRB-RECORDS.
004410
004420
004430 SECTION-EXIT.
004440     EXIT.
004450
004460 1100-RESET-LAST-BILLING     SECTION.
004470
E00619
004490     MOVE  'N'  TO  WS-PROCESS-ERROR-IND
E00619     MOVE  'N'  TO  WS-FORCE-GBM-DATE-ADJUST-IND
004510
E00619**** IF  WS-CONVERSION
E00619****   GO TO  1100-RESET-GBM-AND-SCHEDULE
E00619     .
E00619
004560*        DELETE LBR'S AND LBH FOR LAST MONTH BILLED.
004570*        FOR EACH LBR, POL AND EMP INFO IS UPDATED.
004580     PERFORM 2000-DEL-LASTMTH-LBR-LBH
004590
E00619     IF  WS-PROCESS-ERROR
E00619       GO TO   SECTION-EXIT
E00619     .
E00619
E00619 1100-RESET-GBM-AND-SCHEDULE.
E00619
004660     PERFORM 3000-RESET-GBM-SCHEDULE-REBILL.
004670
E00619     IF  WS-PROCESS-ERROR
E00619       GO TO   SECTION-EXIT
E00619     .
E00619
E00619     ADD 1 TO WS-GBMS-REBILLED
E00619
W01968     PERFORM 4000-RESET-EAD-RECORDS
004750     .
004760 SECTION-EXIT.
004770     EXIT.
004780     EJECT
004790
E00619 1200-MARK-PRDRB-AS-DONE    SECTION.
E00619
004820     MOVE IO-END-BROWSE  TO IO-FUNC
004830     MOVE W-PRD-050-PRODUCT-MASTER-KEY TO IO-KEY
004840     PERFORM IO-DATA-BASE
004850     IF NOT IO-OK
004860       DISPLAY '1200- BAD ENDBR ON PRODUCT     '
004870       PERFORM STANDARD-BATCH-ABEND
004880     .
004890
004900     MOVE W-PRD-050-PRODUCT-MASTER-KEY TO IO-KEY.
004910     MOVE IO-READ-EXACT-LOCK  TO IO-FUNC.
004920     PERFORM IO-DATA-BASE.

004940     IF IO-OK
004950         MOVE IO-AREA
004960           TO W-PRD-PRODUCT-MASTER-DATA
004970     ELSE
004980       DISPLAY '1200- BAD READLOCK ON PRODUCT     '
004990       PERFORM STANDARD-BATCH-ABEND
005000     .

005020     MOVE 'X' TO W-PRD-060-STATUS.

005040     MOVE W-PRD-050-PRODUCT-MASTER-KEY TO IO-KEY.
005050     MOVE W-PRD-PRODUCT-MASTER-DATA    TO IO-AREA.
E00619
E00619     IF  WS-CONVERSION
E00619       MOVE IO-DELETE       TO  IO-FUNC
E00619     ELSE
005100       MOVE IO-WRITE-UPDATE TO IO-FUNC
E00619     .
E00619
005130     PERFORM IO-DATA-BASE.
005140     IF NOT IO-NORMAL
E00619       IF  IO-FUNC = IO-WRITE-UPDATE
E00619         DISPLAY 'BAD WRITE UPDT ON PRDRB '
E00619       ELSE
E00619         DISPLAY 'BAD DELETE ON PRDRB'
E00619       END-IF
E00619       PERFORM STANDARD-BATCH-ABEND
E00619     .

E00619     DISPLAY '1200- PRODUCT STATUS SET TO X'.
E00619
005250 SECTION-EXIT.
005260     EXIT.
005270     EJECT
005280
005290 2000-DEL-LASTMTH-LBR-LBH     SECTION.
005300
005310
005320     MOVE 'L2'                   TO W-LBH-061-RECORD-ID
005330     MOVE ZEROS                  TO W-LBH-062-CLIENT-NUM
E00619***  MOVE RB-PRD-BILLING-FILE-ID TO W-LBH-063-XREF-GROUP-NUM
E00619     MOVE RB-KEY-REBILL-XREF-ID TO W-LBH-063-XREF-GROUP-NUM
005360     MOVE RB-KEY-REBILL-DATE    TO W-LBH-064-BATCH-DATE
005370     MOVE RB-PRD-REBILL-BATCH   TO W-LBH-065-BATCH-NUMBER
005380
005390     MOVE W-LBH-060-LBR-HEADER-ALT-KEY  TO IO-KEY
E00619     MOVE IO-READ-EXACT-INQ             TO IO-FUNC
005410
005420     PERFORM IO-DATA-BASE
005430
005440     IF NOT IO-OK
E00619       IF  RB-PRD-BILLING-FILE-ID  =
E00619                         W-GBM-760-LB-OUTPUT-FILE-ID
005470         DISPLAY 'NO LAST MTH LBH RECS FOR RQST REBILL MTH FOR '
005480           RB-PRD-BILLING-FILE-ID
E00619         IF  WS-CONVERSION
005500           STRING  'NO PRV MTH LBH RECS ON RCBLE HDR TO DELETE '
005510             DELIMITED BY SIZE
005520             INTO  DTL-MESSAGES
005530           PERFORM 8000-ERROR-PRINT
E00619           SET  WS-FORCE-GBM-DATE-ADJUST TO TRUE
E00619         ELSE
005560           STRING  'NO PRV MTH LBH RECS ON RCBLE HDR '
005570                   ' NO REBILL.'
005580             DELIMITED BY SIZE
005590             INTO  DTL-MESSAGES
005600           PERFORM 8000-ERROR-PRINT
W02177           SET  WS-GBM-239-BYPASS-REBILL  TO TRUE
W02154           PERFORM 3000-RESET-GBM-SCHEDULE-REBILL
W02177           SET  WS-GBM-239-DO-REBILL      TO TRUE
E00619           SET  WS-PROCESS-ERROR  TO TRUE
E00619         END-IF
005660         GO TO  SECTION-EXIT
E00619       ELSE
E00619*             NO DELETES - JUST ALLOW UPDATE GBM FOR B9020 SELECT
E00619*                          BY NOT SETTING WS-PROCESS-ERROR TRUE
005700         DISPLAY 'NO LBH RECS FOR REQUEST REBILL MTH FOR '
005710           RB-PRD-BILLING-FILE-ID
E00619           ', THIS SUBGROUP ALREADY PROCESSED UNDER '
E00619           W-GBM-760-LB-OUTPUT-FILE-ID
005740         STRING  'BILLING GROUP DONE UNDER XREF GRP '
005750                 W-GBM-760-LB-OUTPUT-FILE-ID
005760           DELIMITED BY SIZE
005770           INTO  DTL-MESSAGES
005780         PERFORM 8000-ERROR-PRINT
E00619         MOVE 'GBM FOR SUBGROUP RESET FOR BILLING' TO DTL-MESSAGES
E00619         IF  WS-CONVERSION
E00619           SET  WS-FORCE-GBM-DATE-ADJUST TO TRUE
E00619         END-IF
E00619******   SET  WS-PROCESS-ERROR  TO TRUE
005840         GO TO  SECTION-EXIT
E00619       END-IF
005860     .
005870
005880     IF NOT IO-OK
005890       DISPLAY 'BAD REQD INQ LBH ' W-LBH-060-LBR-HEADER-ALT-KEY
005900       MOVE    SPACES TO O-PRT-DETAIL-LINE
005910       STRING  'BAD STARTBR ON RECEIVABLE HDR '
005920               ' NO REBILL.'
005930         DELIMITED BY SIZE
005940         INTO  DTL-MESSAGES
005950       PERFORM 8000-ERROR-PRINT
E00619       SET  WS-PROCESS-ERROR  TO TRUE
005970       GO TO   SECTION-EXIT
005980     .
005990
006000     MOVE IO-AREA  TO  W-LBH-LBR-HEADER-DATA
006010
006020     DISPLAY 'LBH-061= ' W-LBH-061-RECORD-ID
006030      ' LBH-063= ' W-LBH-063-XREF-GROUP-NUM
E00619      ' RB-PRD= 'RB-PRD-BILLING-FILE-ID
006050
006060     IF  W-LBH-061-RECORD-ID = 'L2'
006070     AND W-LBH-063-XREF-GROUP-NUM = W-GBM-760-LB-OUTPUT-FILE-ID
006080     AND W-GBM-120-LAST-BILL-DATE  =  W-LBH-064-BATCH-DATE
006090     AND W-GBM-125-LAST-BILL-BATCH =  W-LBH-065-BATCH-NUMBER
006100       NEXT SENTENCE
006110     ELSE
E00619       SET  WS-PROCESS-ERROR  TO TRUE
006130       MOVE    SPACES TO O-PRT-DETAIL-LINE
006140       STRING  'REQUESTED REBILL MONTH RECVBL HDR NOT FOUND'
006150       DELIMITED BY SIZE
006160       INTO    DTL-MESSAGES
006170       PERFORM 8000-ERROR-PRINT
006180       GO TO  SECTION-EXIT
006190     .
006200
006210*           POLICY AND EMP DEDUCTION HIST UPDATED IN 2500
006220     PERFORM 2500-DELETE-LBR-RECORDS
006230
006240     IF  WS-PROCESS-ERROR
006250         GO TO   SECTION-EXIT
006260     .
006270
W02154     MOVE W-LBH-220-TOT-PD-SINCE-LAST-BL
W02154       TO WS-HOLD-LBH-220-TOT-PD
W02154
006310     PERFORM 2100-DELETE-LBH
006320
006330     IF  WS-PROCESS-ERROR
006340         GO TO   SECTION-EXIT
006350     .
006360
006370 SECTION-EXIT.
006380     EXIT.
006390     EJECT
006400
006410 2100-DELETE-LBH    SECTION.
006420
006430     MOVE IO-END-BROWSE  TO IO-FUNC
006440     MOVE W-LBH-060-LBR-HEADER-ALT-KEY TO IO-KEY
006450     PERFORM IO-DATA-BASE
006460     IF NOT IO-OK
006470       DISPLAY '2100- ERR ON END-BR TO UPDT LBH'
006480       PERFORM STANDARD-BATCH-ABEND
006490     .
006500
006510     MOVE W-LBH-050-LBR-HEADER-KEY      TO IO-KEY
006520     MOVE IO-READ-EXACT-LOCK            TO IO-FUNC
006530     PERFORM IO-DATA-BASE
006540     IF NOT IO-OK
006550       DISPLAY '2100- ERR ON READ-LOCK TO UPDT LBH'
006560       PERFORM STANDARD-BATCH-ABEND
006570     .
006580
006590     MOVE  W-LBH-LBR-HEADER-DATA         TO IO-AREA
006600     MOVE  W-LBH-050-LBR-HEADER-KEY      TO IO-KEY
006610     MOVE  IO-DELETE                     TO IO-FUNC
006620
006630     PERFORM IO-DATA-BASE
006640     IF NOT IO-OK
006650       DISPLAY '2100- ERR ON DELETE OF LBH  '
006660       PERFORM STANDARD-BATCH-ABEND
006670     .
006680
E00619     DISPLAY 'LBH DELETED ' W-LBH-050-LBR-HEADER-KEY.
E00619
006710 SECTION-EXIT.
006720     EXIT.
006730     EJECT
006740
006750 2500-DELETE-LBR-RECORDS     SECTION.
006760
006770     MOVE SPACES                     TO W-LBR-060-RECEIVE-ALT-KEY
006780     MOVE 'L1'                       TO W-LBR-061-RECORD-ID
006790     MOVE W-LBH-053-BATCH-DATE       TO W-LBR-062-BATCH-DATE
006800     MOVE W-LBH-054-BATCH-NUMBER     TO W-LBR-063-BATCH-NUMBER
006810**        AS OF W01649, XREF WILL BE IN LBR064 SO IT CAN BE USED
006820     MOVE W-LBH-052-XREF-GROUP-NUM   TO W-LBR-064-GROUP-NUMBER
006830
006840     MOVE +25                        TO IO-KEY-LTH
006850     MOVE W-LBR-060-RECEIVE-ALT-KEY  TO IO-KEY
006860     MOVE IO-START-BROWSE-GTEQ       TO IO-FUNC
006870     DISPLAY '2500 START BR = ' W-LBR-060-RECEIVE-ALT-KEY
006880
006890     PERFORM IO-DATA-BASE
006900
006910     IF IO-ENDFILE OR IO-NOTFND
006920        GO TO SECTION-EXIT
006930     ELSE
006940     IF NOT IO-OK
006950        SET  WS-PROCESS-ERROR   TO  TRUE
006960        DISPLAY 'BAD LBR STBR    : ' W-LBR-060-RECEIVE-ALT-KEY
006970        MOVE    SPACES TO O-PRT-DETAIL-LINE
006980        STRING  'UNABLE TO START READ OF RECEIVABLE LBR '
006990        DELIMITED BY SIZE
007000        INTO    DTL-MESSAGES
007010        PERFORM 8000-ERROR-PRINT
007020        GO TO   SECTION-EXIT
007030     .
007040
007050 2510-READ-NEXT-LBR.
007060
007070     MOVE W-LBR-060-RECEIVE-ALT-KEY  TO IO-KEY
007080     MOVE IO-READ-NEXT               TO IO-FUNC
007090
007100     PERFORM IO-DATA-BASE
007110
007120     IF IO-ENDFILE OR IO-NOTFND
007130        GO TO SECTION-EXIT
007140     ELSE
007150     IF NOT IO-OK
007160       SET  WS-PROCESS-ERROR   TO  TRUE
007170       DISPLAY 'BAD LBR READ NEXT ' W-LBR-060-RECEIVE-ALT-KEY
007180       MOVE    SPACES TO O-PRT-DETAIL-LINE
007190       STRING  'UNABLE TO READ NEXT RECEIVABLE LBR'
007200        DELIMITED BY SIZE
007210       INTO    DTL-MESSAGES
007220       PERFORM 8000-ERROR-PRINT
007230       PERFORM STANDARD-BATCH-ABEND
007240     .
007250
007260     MOVE IO-AREA                    TO W-LBR-RECEIVABLES-DATA
007270
007280*          COMPARE PRIM KEY AS ALT 064-GROUP-NUMBER MAY CONTAIN
007290*          GROUP NUMBERS COMBINED WITH THE PRIMARY GROUP NUMBER.
007300*          (EX: 0621406001 PRIM HAS 0621406001,2,3,4
E00619*           NOT TRUE AS OF W01649 - HAS XREF
007320     IF  'LB'                     = W-LBR-051-RECORD-ID
007330     AND W-LBH-053-BATCH-DATE     = W-LBR-054-BATCH-DATE
007340     AND W-LBH-054-BATCH-NUMBER   = W-LBR-055-BATCH-NUMBER
007350     AND W-LBH-052-XREF-GROUP-NUM = W-LBR-052-XREF-GROUP-NUM
007360       NEXT SENTENCE
007370     ELSE
007380       GO TO SECTION-EXIT
007390     .
007400
E00619     MOVE  W-LBR-060-RECEIVE-ALT-KEY  TO WS-HOLD-LBR-FROM-DELETE
E00619     .
E00619
007440 2515-DEL-LBR-UPDT-POL-EMD.
007450
007460     PERFORM 2600-DELETE-LBR
007470
E00619*              RESET BILLED NEXT PERIOD IND TO 'N'
E00619*              ON PREV LBR TO THE ONE JUST DELETED - USING
E00619*              PRIMARY KEY WITH GBM PREV BATCH DATE,NUM
E00619
007520*              THIS IS DONE SO B9070 SETS THIS FIELD BASED ON THE
007530*              REBILLED LBR's.
007540     PERFORM 2650-RSET-PREV-LBR-200

W03406*    MOVE W-LBR-240-COMPANY-CODE   TO WS-CO-FOR-POL-READ
W03406*    MOVE W-LBR-250-POLICY-NUMBER  TO WS-POL-FOR-POL-READ
W03406     MOVE WS-HOLD-CO-FOR-POL-READ  TO WS-CO-FOR-POL-READ
W03406     MOVE WS-HOLD-POL-FOR-POL-READ TO WS-POL-FOR-POL-READ
W03406
007610     PERFORM 2700-UPDATE-POLICY-INFO
007620
W02391     IF  W-GBM-438-BILL-TYPE-IND = 'P'
W02391         CONTINUE
W02391     ELSE
007660         PERFORM 2800-UPDATE-EMP-DED-HIST-INFO
W02391     END-IF
007680
E00619     MOVE  WS-HOLD-LBR-FROM-DELETE TO  W-LBR-060-RECEIVE-ALT-KEY
E00619
007710     MOVE W-LBR-060-RECEIVE-ALT-KEY  TO IO-KEY
007720     MOVE IO-RESET-BROWSE-GTEQ          TO IO-FUNC

007740     PERFORM IO-DATA-BASE
007750     IF  NOT IO-OK
007760       SET  WS-PROCESS-ERROR   TO  TRUE
007770       DISPLAY 'ERR RESET BR ON LBR '
007780         W-LBR-060-RECEIVE-ALT-KEY
007790       MOVE    SPACES TO O-PRT-DETAIL-LINE
007800       STRING  'UNABLE TO RESETBR READ RECEIVABLE     '
007810         DELIMITED BY SIZE
007820       INTO    DTL-MESSAGES
007830       PERFORM 8000-ERROR-PRINT
007840       PERFORM STANDARD-BATCH-ABEND
007850     .
007860
007870     GO TO 2510-READ-NEXT-LBR
007880     .
007890
007900
007910 SECTION-EXIT.
007920     EXIT.
007930     EJECT
007940
007950 2600-DELETE-LBR     SECTION.
007960
W03406     INITIALIZE WS-HOLD-CO-FOR-POL-READ
W03406                WS-HOLD-POL-FOR-POL-READ
W03406
008000     MOVE IO-END-BROWSE  TO IO-FUNC
008010     MOVE W-LBR-060-RECEIVE-ALT-KEY TO IO-KEY
008020     PERFORM IO-DATA-BASE
008030     IF NOT IO-OK
008040       SET  WS-PROCESS-ERROR   TO  TRUE
008050       PERFORM STANDARD-BATCH-ABEND
008060     .
008070
008080     MOVE W-LBR-050-RECEIVABLES-KEY     TO IO-KEY
008090     MOVE IO-READ-EXACT-LOCK            TO IO-FUNC
008100     PERFORM IO-DATA-BASE
008110     IF NOT IO-OK
008120       DISPLAY '2600- ERR ON READ-LOCK TO DELETE LBR'
008130       PERFORM STANDARD-BATCH-ABEND
008140     .
008150
W03316     MOVE  W-LBR-053-RECON-KEY            TO WS-HOLD-RECON-KEY
008170     MOVE  W-LBR-RECEIVABLES-DATA         TO IO-AREA
W03406     MOVE  W-LBR-240-COMPANY-CODE         TO
W03406                                        WS-HOLD-CO-FOR-POL-READ
W03406     MOVE  W-LBR-250-POLICY-NUMBER        TO
W03406                                        WS-HOLD-POL-FOR-POL-READ
008220     MOVE  W-LBR-050-RECEIVABLES-KEY      TO IO-KEY
008230     MOVE  IO-DELETE                      TO IO-FUNC
008240
008250     PERFORM IO-DATA-BASE
008260     IF NOT IO-OK
008270       DISPLAY '2600- ERR ON DELETE OF LBR  '
008280       PERFORM STANDARD-BATCH-ABEND
008290     .
008300
E00619     DISPLAY 'LBR DELETED ' W-LBR-050-RECEIVABLES-KEY.
008320 SECTION-EXIT.
008330     EXIT.
008340     EJECT
008350
E00619 2650-RSET-PREV-LBR-200     SECTION.
E00619
E00619*           USE "PREV" FROM GBM AS GBM HAS NOT BEEN UPDATED
E00619*           WITH THE ROLLBACK REBILL DATES
008400     MOVE W-GBM-150-PREV-BILL-DATE   TO W-LBR-054-BATCH-DATE
008410     MOVE W-GBM-155-PREV-BILL-BATCH  TO W-LBR-055-BATCH-NUMBER
W03406     INITIALIZE W-LBR-056-SEQUENCE-NUMBER
W03406                W-LBR-057-SUB-SEQUENCE

W01646     DISPLAY '2650- GBM READ KEY= ' W-LBR-050-RECEIVABLES-KEY
W01646
E00619     IF  W-GBM-150-PREV-BILL-DATE = ZEROS
E00619       DISPLAY '2650- NO PREV BILL DTE ON GBM FOR RESET'
E00619       GO TO  SECTION-EXIT
E00619     .
E00619
W03316*    MOVE W-LBR-050-RECEIVABLES-KEY  TO IO-KEY
W03316*    MOVE IO-READ-EXACT-LOCK         TO IO-FUNC

W03316*    PERFORM IO-DATA-BASE

W03316*    IF IO-NOTFND
W03316*      DISPLAY '2650- NO PREV MTH LBR FOR RESET '
W03316*        W-LBR-050-RECEIVABLES-KEY
W03316*      GO TO   SECTION-EXIT
W03316*    .
W03316*
W03316*    IF IO-ENDFILE
W03316*      DISPLAY '2650- ENDFILE/NOTFND ON READ GENERIC'
W03316*      GO TO SECTION-EXIT
W03316*    ELSE
W03316*       IF NOT IO-OK
W03316*          DISPLAY '2650- PROBLEM WITH LBR: '
W03316*                W-LBR-060-RECEIVE-ALT-KEY
W03316*          PERFORM STANDARD-BATCH-ABEND
W03316*    .

W03316*    MOVE IO-AREA                    TO W-LBR-RECEIVABLES-DATA

W03316     MOVE +40                        TO IO-KEY-LTH
W03316     MOVE W-LBR-050-RECEIVABLES-KEY  TO IO-KEY
W03316     MOVE IO-READ-GENERIC-GTEQ       TO IO-FUNC
W03316     PERFORM IO-DATA-BASE
W03316     IF IO-ENDFILE OR IO-NOTFND
W03316       DISPLAY '2650- ENDFILE/NOTFND ON READ GENERIC'
W03316       GO TO SECTION-EXIT
W03316     ELSE
W03316       IF NOT IO-OK
W03316         DISPLAY 'PROBLEM WITH LBR: ' W-LBR-050-RECEIVABLES-KEY
W03316         PERFORM STANDARD-BATCH-ABEND
W03316     .
W03316    MOVE IO-AREA     TO W-LBR-RECEIVABLES-DATA
W03316
W03316    IF  W-LBR-051-RECORD-ID       =  'LB'
W03316    AND W-LBR-052-XREF-GROUP-NUM  =  W-LBH-052-XREF-GROUP-NUM
W03316    AND W-LBR-053-RECON-KEY       =  WS-HOLD-RECON-KEY
W03316    AND W-LBR-054-BATCH-DATE      =  W-GBM-150-PREV-BILL-DATE
W03316    AND W-LBR-055-BATCH-NUMBER    =  W-GBM-155-PREV-BILL-BATCH
W03316        NEXT SENTENCE
W03316    ELSE
W03316        GO TO SECTION-EXIT
W03316    .
E00619     DISPLAY 'LBR200 WAS ' W-LBR-200-BILLED-NEXT-PD
E00619
009000     MOVE 'N'   TO W-LBR-200-BILLED-NEXT-PD

TEST       DISPLAY '2650 UPDATE PREV lbr = ' W-LBR-050-RECEIVABLES-KEY
E00619       ' LBR200 SET TO ' W-LBR-200-BILLED-NEXT-PD
E00619
W03406     MOVE W-LBR-050-RECEIVABLES-KEY TO IO-KEY
W03316     MOVE W-LBR-RECEIVABLES-DATA    TO IO-AREA
W03316     MOVE IO-READ-EXACT-LOCK        TO IO-FUNC
W03316     PERFORM IO-DATA-BASE
W03316
W03316     IF NOT IO-OK
W03316        DISPLAY 'BAD LBR LOCK: ' W-LBR-050-RECEIVABLES-KEY
W03316        GO TO SECTION-EXIT
W03316     .
W03316
W03406     MOVE W-LBR-050-RECEIVABLES-KEY TO IO-KEY
009160     MOVE W-LBR-RECEIVABLES-DATA    TO IO-AREA
009170     MOVE IO-WRITE-UPDATE           TO IO-FUNC

009190     PERFORM IO-DATA-BASE

009210     IF NOT IO-OK
009220        DISPLAY '2650-BAD LBR WU : ' W-LBR-050-RECEIVABLES-KEY
009230        PERFORM STANDARD-BATCH-ABEND
009240     .
E00619     DISPLAY 'LBR UPDATED ' W-LBR-050-RECEIVABLES-KEY.
009260
009270 SECTION-EXIT.
009280     EXIT.
009290     EJECT
009300
009310 2700-UPDATE-POLICY-INFO     SECTION.
009320
009330     INITIALIZE W-POL-060-POLICY-NUMBER-KEY
009340     MOVE  'PN'                     TO  W-POL-062-RECORD-ID
009350     DISPLAY '2700- CO= ' WS-CO-FOR-POL-READ     ' POL= '
009360       WS-POL-FOR-POL-READ     ' FOR UPDATE'
009370     MOVE  WS-CO-FOR-POL-READ       TO  W-POL-064-COMPANY-CODE
009380     MOVE  WS-POL-FOR-POL-READ      TO  W-POL-066-POLICY-NUMBER
009390     MOVE  W-POL-060-POLICY-NUMBER-KEY TO IO-KEY
009400     MOVE  IO-READ-EXACT-INQ           TO IO-FUNC
009410
009420     PERFORM IO-DATA-BASE
009430
009440     IF NOT IO-OK
009450       DISPLAY 'BAD POL READ ' W-POL-060-POLICY-NUMBER-KEY
009460       PERFORM STANDARD-BATCH-ABEND
009470     .
009480
E00619     MOVE  IO-AREA TO W-POL-POLICY-MASTER-DATA
E00619
009510     MOVE  W-POL-050-POLICY-MASTER-KEY TO IO-KEY
009520     MOVE  IO-READ-EXACT-LOCK          TO IO-FUNC
009530
009540     PERFORM IO-DATA-BASE
009550
009560     IF NOT IO-OK
009570       DISPLAY 'BAD POL READ LOCK ' W-POL-050-POLICY-MASTER-KEY
009580       PERFORM STANDARD-BATCH-ABEND
009590     .
009600
009610     MOVE IO-AREA  TO  W-POL-POLICY-MASTER-DATA
009620
009630     MOVE RB-KEY-REBILL-DATE TO W-POL-248-BILL-TO-DATE
009640
E00619     DISPLAY 'POL248 SET TO ' W-POL-248-BILL-TO-DATE
E00619
009670     IF  W-POL-248-BILL-TO-DATE < W-POL-500-PAID-TO-DATE
E00619       DISPLAY 'POL248 RESET TO POL500 ' W-POL-500-PAID-TO-DATE
009690       MOVE W-POL-500-PAID-TO-DATE TO W-POL-248-BILL-TO-DATE
009700     .
009710
009720     MOVE  W-POL-POLICY-MASTER-DATA    TO IO-AREA
009730     MOVE  W-POL-050-POLICY-MASTER-KEY TO IO-KEY
009740     MOVE  IO-WRITE-UPDATE             TO IO-FUNC
009750
009760     PERFORM IO-DATA-BASE
009770
009780     IF NOT IO-OK
009790       DISPLAY 'BAD POL UPDATE    ' W-POL-050-POLICY-MASTER-KEY
009800       PERFORM STANDARD-BATCH-ABEND
009810     .
009820
E00619     DISPLAY 'POLICY UPDATED ' W-POL-060-POLICY-NUMBER-KEY.
009840 SECTION-EXIT.
009850     EXIT.
009860     EJECT
009870
009880 2800-UPDATE-EMP-DED-HIST-INFO     SECTION.
009890
009900
009910     INITIALIZE W-EMD-030-EMP-DED-HIST-KEY
009920
009930     MOVE 'ED'                        TO W-EMD-032-RECORD-ID
009940     MOVE W-POL-052-ACCOUNT-TYPE      TO W-EMD-034-ACCOUNT-TYPE
009950     MOVE W-POL-053-ACCOUNT-NUMBER    TO W-EMD-036-ACCOUNT-NUMBER
009960     MOVE W-POL-304-PRIMARY-EMPLOYEE  TO W-EMD-038-RECORD-NUMBER
009970
009980     MOVE W-EMD-030-EMP-DED-HIST-KEY TO IO-KEY
009990     MOVE IO-READ-EXACT-LOCK    TO IO-FUNC
010000
010010     PERFORM IO-DATA-BASE
010020
010030     IF NOT IO-OK
W03267***      MOVE 2231 TO WS-USER-CODE
010050         DISPLAY '2800- NO EMD REC FOR EMP: '
010060                      W-EMD-030-EMP-DED-HIST-KEY
W03267***      PERFORM STANDARD-BATCH-ABEND
W03267         STRING  'EMD REC FOR DED ROLLBACK NOT FOUND.'
W03267           DELIMITED BY SIZE
W03267           INTO  DTL-MESSAGES
W03267         PERFORM 8000-ERROR-PRINT
W03267         GO TO   SECTION-EXIT
010130     .
010140
010150     MOVE IO-AREA               TO W-EMD-EMPLOYEE-DED-HIST-DATA
010160
010170     DISPLAY '2800 REMOVE DED= ' W-EMD-130-DED-AMT-BILLED (1)
010180        ' NEW 1ST DED IS   = ' W-EMD-130-DED-AMT-BILLED (2)
010190
E00619     PERFORM 2810-ROLLBACK-DED-HISTORY
E00619
010220     MOVE W-EMD-030-EMP-DED-HIST-KEY   TO IO-KEY
010230     MOVE W-EMD-EMPLOYEE-DED-HIST-DATA TO IO-AREA
010240     MOVE IO-WRITE-UPDATE              TO IO-FUNC
010250
010260     PERFORM IO-DATA-BASE
010270
010280     IF NOT IO-OK
010290        MOVE 2232 TO WS-USER-CODE
010300        DISPLAY '2280- BAD EMD WRITE UPD : '
010310              W-EMD-030-EMP-DED-HIST-KEY
010320        PERFORM STANDARD-BATCH-ABEND
010330     .
010340
E00619     DISPLAY 'EMD HIST UPDATED ' W-EMD-EMPLOYEE-DED-HIST-DATA.
010360 SECTION-EXIT.
010370     EXIT.
010380     EJECT
010390
E00619 2810-ROLLBACK-DED-HISTORY      SECTION.
E00619
E00619     PERFORM VARYING SUB FROM 2 BY 1
E00619               UNTIL SUB > 20
E00619       IF  W-EMD-140-DATE-DED-AMT-BILLED (SUB - 1) =
E00619           RB-KEY-REBILL-DATE
E00619         PERFORM VARYING SUB1 FROM SUB BY 1
E00619                   UNTIL SUB1 > 20
E00619           MOVE W-EMD-120-DEDUCTION-DATA (SUB1)
E00619             TO W-EMD-120-DEDUCTION-DATA (SUB1 - 1)
E00619         END-PERFORM
E00619         MOVE SPACES TO W-EMD-120-DEDUCTION-DATA (20)
E00619         MOVE 20 TO SUB
E00619       END-IF
E00619     END-PERFORM
010550     .
010560 SECTION-EXIT.
010570     EXIT.
010580     EJECT
010590
E00619 3000-RESET-GBM-SCHEDULE-REBILL     SECTION.
E00619*    USE "SET  WS-PROCESS-ERROR  TO TRUE" IF NOT ABORTING JOB
E00619
E00619*         USE RB-PRD-BILLING-FILE-ID (WILL BE XREF OR SUB GROUP)
E00619*         SO THE SUBGROUPS GET SET FOR BILLING IN B9020
E00619
010660     MOVE 'GB'                   TO W-GBM-051-RECORD-ID
010670     MOVE '05'                   TO W-GBM-052-GROUP-BLG-TYPE
E00619     MOVE RB-PRD-BILLING-FILE-ID TO W-GBM-053-GROUP-NUMBER
E00619***  MOVE W-GBM-760-LB-OUTPUT-FILE-ID TO W-GBM-053-GROUP-NUMBER
010700     MOVE RB-KEY-BILLING-DAY     TO W-GBM-054-PAYMENT-DAY

010720     MOVE W-GBM-050-GROUP-BILLING-KEY TO IO-KEY
E00619
010740     MOVE IO-READ-EXACT-LOCK     TO IO-FUNC

010760     PERFORM IO-DATA-BASE

010780     IF NOT IO-OK
010790        DISPLAY 'NO GBM REC:' W-GBM-050-GROUP-BILLING-KEY
010800        PERFORM STANDARD-BATCH-ABEND
010810     .
010820     MOVE IO-AREA              TO W-GBM-GROUP-BLG-MASTER-DATA
E00619
E00619     .
E00619
W02177     IF WS-GBM-239-BYPASS-REBILL
W02154        MOVE 'N'  TO  W-GBM-239-GRP-SALDED-REBILL
W02154        GO TO 3000-SCHEDULE-REBILL
W02154     END-IF
W02154
E00619**      ONLY SET TO 'C' FOR CONVERSION RUN
E00619**      IDENTIFIES A CONVERSION REBILL REQUEST FOR B9020
E00619**      INSTEAD OF A NORMAL ONLINE REQUESTED REBILL (=Y)
E00619     IF  WS-CONVERSION
E00619       MOVE 'C'      TO W-GBM-239-GRP-SALDED-REBILL
E00619       DISPLAY '3000- GBM239= ' W-GBM-239-GRP-SALDED-REBILL
E00619****** GO TO  3000-SCHEDULE-REBILL
010980     .

E00619     IF  WS-FORCE-GBM-DATE-ADJUST
E00619       DISPLAY 'GBM FORCE RESET TO REBILL DATE - CONVERSION - NO P
E00619-      'REV DATES'
E00619       PERFORM 3100-NO-PRV-LBR-RSET-POL-BILL2
011040       IF  W-GBM-120-LAST-BILL-DATE = ZEROS
E00619         MOVE RB-KEY-REBILL-DATE    TO W-GBM-120-LAST-BILL-DATE
E00619         MOVE ZEROS                 TO W-GBM-125-LAST-BILL-BATCH
E00619                                       W-GBM-140-LAST-BILL-AMT
E00619       END-IF
E00619       GO TO  3000-SCHEDULE-REBILL
E00619     .
E00619
E00619     DISPLAY ' GBM RESETS ARE:'
E00619     DISPLAY 'LAST WAS  ' W-GBM-120-LAST-BILL-DATE
E00619     DISPLAY 'PREV BILL ' W-GBM-150-PREV-BILL-DATE
E00619             ' TO LAST '
E00619     DISPLAY '2ND PRV TO PREV ' W-GBM-428-2ND-PREV-BILL-DATE
E00619        ' TO PREV'.
E00619     MOVE W-GBM-150-PREV-BILL-DATE  TO W-GBM-120-LAST-BILL-DATE
E00619     MOVE W-GBM-155-PREV-BILL-BATCH TO W-GBM-125-LAST-BILL-BATCH
E00619     MOVE W-GBM-170-PREV-BILL-AMT   TO W-GBM-140-LAST-BILL-AMT
E00619     MOVE W-GBM-428-2ND-PREV-BILL-DATE TO W-GBM-150-PREV-BILL-DATE
E00619     MOVE W-GBM-430-2ND-PREV-BILL-BATCH
E00619                                    TO W-GBM-155-PREV-BILL-BATCH
E00619     MOVE W-GBM-432-2ND-PREV-BILL-AMT
E00619                                    TO W-GBM-170-PREV-BILL-AMT
E00619     .
E00619 3000-SCHEDULE-REBILL.
E00619
W02177     IF WS-GBM-239-DO-REBILL
E00619     MOVE RB-KEY-REBILL-DATE     TO W-GBM-520-NEXT-BILL-DATE
W02154     MOVE WS-HOLD-LBH-220-TOT-PD TO W-GBM-806-TOT-PD-SINCE-LAST-BL
W02177     .
E00619
E00619     DISPLAY 'GBM520-NEXT BILL = ' W-GBM-520-NEXT-BILL-DATE
E00619      ' GBM239= ' W-GBM-239-GRP-SALDED-REBILL
E00619
011370     MOVE W-GBM-GROUP-BLG-MASTER-DATA TO IO-AREA
011380     MOVE W-GBM-050-GROUP-BILLING-KEY TO IO-KEY
011390     MOVE IO-WRITE-UPDATE             TO IO-FUNC

011410     PERFORM IO-DATA-BASE

011430     IF NOT IO-OK
011440        MOVE 2092 TO WS-USER-CODE
011450        DISPLAY 'BAD GBM WU   :' W-GBM-050-GROUP-BILLING-KEY
011460        PERFORM STANDARD-BATCH-ABEND
011470     .

011490 SECTION-EXIT.
011500     EXIT.
011510     EJECT
011520
E00619 3100-NO-PRV-LBR-RSET-POL-BILL2    SECTION.
E00619
E00619     DISPLAY 'RESETTING POLS USING ING FOR GBM '
E00619        W-GBM-053-GROUP-NUMBER ' ' W-GBM-054-PAYMENT-DAY
E00619
011580     INITIALIZE  W-ING-070-GROUP-TYPE-KEY

011600     MOVE 'I2'                     TO W-ING-071-RECORD-ID
011610     MOVE 05                       TO W-ING-072-METHOD
011620     MOVE W-GBM-053-GROUP-NUMBER   TO W-ING-078-GRP-NUMBER
011630     MOVE W-GBM-054-PAYMENT-DAY    TO W-ING-064-PAYMENT-DAY
E00619     MOVE SPACES                   TO W-ING-087-COMPANY-NUM
E00619                                      W-ING-088-POLICY-NUM
011660     MOVE W-ING-070-GROUP-TYPE-KEY TO IO-KEY
011670     MOVE IO-START-BROWSE-GTEQ     TO IO-FUNC
011680     MOVE '310001'                 TO IO-IDENTIFIER

011700     PERFORM IO-DATA-BASE
011710     IF NOT IO-OK
011720         DISPLAY '********************************************'
011730         DISPLAY '310001 - IO ERROR START-BROWSE "ING" RECORDS'
011740         DISPLAY 'LOOKING FOR INGS IN GROUP TO RESET POL BILLTO. '
011750         DISPLAY ' ING KEY= ' W-ING-070-GROUP-TYPE-KEY
011760         DISPLAY ' '
011770         DISPLAY 'IO-RETURN-IND = ' IO-RETURN-IND
011780         DISPLAY 'IO-ERROR-AREA = ' IO-ERROR-AREA
011790         DISPLAY 'IO-KEY        = ' IO-KEY
011800         DISPLAY ' '
011810         DISPLAY 'CANNOT BE DONE FOR GBM: ' W-GBM-053-GROUP-NUMBER
011820         DISPLAY '              BILL DAY: ' W-GBM-054-PAYMENT-DAY
011830         DISPLAY ' '
011840         DISPLAY '********************************************'
E00619         PERFORM STANDARD-BATCH-ABEND
011860         GO TO SECTION-EXIT
011870     .

011890 3105-READ-NEXT-ING.

011910     MOVE W-ING-070-GROUP-TYPE-KEY   TO IO-KEY
011920     MOVE IO-READ-NEXT               TO IO-FUNC
011930     MOVE '310502'                   TO IO-IDENTIFIER

011950     PERFORM IO-DATA-BASE
011960     IF NOT IO-OK
011970         IF  IO-ENDFILE
011980          OR IO-NOTFND
011990             DISPLAY '*******************************************'
012000             DISPLAY 'IO-ERROR READ-NEXT "ING" RECORD'
012010             DISPLAY 'LOOKING FOR ING TO RESET POL BILL-TO      '
012020             DISPLAY 'EOF/NOT FOUND - NO VALID "ING" RECORD FOUND'
012030             DISPLAY ' '
012040             DISPLAY '*******************************************'
E00619             GO TO SECTION-EXIT
012060         ELSE
012070             DISPLAY '*******************************************'
012080             DISPLAY 'IO-ERROR READ-NEXT "ING" RECORD'
012090             DISPLAY 'LOOKING FOR POLS ON GBM BEING REBILLED '
012100             DISPLAY ' '
012110             DISPLAY 'IO-RETURN-IND = ' IO-RETURN-IND
012120             DISPLAY 'IO-ERROR-AREA = ' IO-ERROR-AREA
012130             DISPLAY 'IO-KEY        = ' IO-KEY
012140             DISPLAY ' '
012150             DISPLAY 'CANNOT BE DONE FOR GBM: '
012160                                         W-GBM-053-GROUP-NUMBER
012170             DISPLAY '              BILL DAY: '
012180                                         W-GBM-054-PAYMENT-DAY
012190             DISPLAY ' '
012200             DISPLAY '*******************************************'
012210         END-IF
012220         PERFORM STANDARD-BATCH-ABEND
012230     .

012250     MOVE IO-AREA    TO W-ING-INDIVIDUAL-GROUP-DATA

012270     IF   (W-ING-071-RECORD-ID          = 'I2')
012280      AND (W-ING-072-METHOD             = '05')
012290      AND (W-ING-078-GRP-NUMBER         = W-GBM-053-GROUP-NUMBER)
012300      AND (W-ING-064-PAYMENT-DAY        = W-GBM-054-PAYMENT-DAY)
012310         NEXT SENTENCE
012320     ELSE
012330         GO TO SECTION-EXIT
012340     .

012360     IF W-ING-090-STATUS = 'A01' OR 'P12'
012370         NEXT SENTENCE
012380     ELSE
E00619         DISPLAY 'BYPASS ING STATUS ' W-ING-090-STATUS
E00619           ' POL= ' W-ING-088-POLICY-NUM
012410         GO TO 3105-READ-NEXT-ING
012420     .

012440 3105-READ-POLICY.

E00619     MOVE W-ING-087-COMPANY-NUM  TO WS-CO-FOR-POL-READ
E00619     MOVE W-ING-088-POLICY-NUM   TO WS-POL-FOR-POL-READ
E00619
E00619     PERFORM 2700-UPDATE-POLICY-INFO

012510     GO TO 3105-READ-NEXT-ING
012520     .
E00619
012540 SECTION-EXIT.
012550     EXIT.
012560     EJECT
012570
W01968 4000-RESET-EAD-RECORDS SECTION.
W01968
W01968     INITIALIZE W-EAD-EMP-ADJUSTMENTS-DATA
W01968     MOVE 'EA'                   TO W-EAD-051-RECORD-ID
W01968     MOVE RB-PRD-BILLING-FILE-ID TO W-EAD-052-GROUP-NUMBER
W01968     MOVE ZERO                   TO W-EAD-054-CMPL-SEQ-NUMBER
W01968
W01968     MOVE W-EAD-050-EMP-ADJUSTMENTS-KEY TO IO-KEY
W01968     MOVE IO-START-BROWSE-GTEQ          TO IO-FUNC
W01968     PERFORM IO-DATA-BASE
W01968
W01968     IF NOT IO-OK
W01968        DISPLAY '$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$'
W01968        DISPLAY 'BAD START ON EAD: ' W-EAD-050-EMP-ADJUSTMENTS-KEY
W01968        DISPLAY ' DB AREA = ' DB-IO-AREA
W01968        DISPLAY 'ADJUSTMENT NOT PROCESSED FOR: '
W01968                                           W-GBM-053-GROUP-NUMBER
W01968        DISPLAY '$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$'
W01968        GO TO SECTION-EXIT
W01968     END-IF.
W01968
W01968 4000-READ-NEXT-EAD.
W01968
W01968     MOVE IO-READ-NEXT               TO IO-FUNC
W01968
W01968     PERFORM IO-DATA-BASE
W01968
W01968     MOVE IO-AREA                    TO W-EAD-EMP-ADJUSTMENTS-DATA
W01968
W01968     IF  IO-OK
W01968     AND IO-NORMAL
W01968     AND W-EAD-052-GROUP-NUMBER = RB-PRD-BILLING-FILE-ID
W01968        CONTINUE
W01968     ELSE
W01968        GO TO SECTION-EXIT
W01968     END-IF
W01968
W01968     IF W-EAD-200-ADJ-BILLED-DATE = RB-KEY-REBILL-DATE
W01968        CONTINUE
W01968     ELSE
W01968        GO TO 4000-READ-NEXT-EAD
W01968     END-IF
W01968
W01968     MOVE W-EAD-050-EMP-ADJUSTMENTS-KEY TO IO-KEY
W01968     MOVE IO-READ-EXACT-LOCK            TO IO-FUNC
W01968
W01968     PERFORM IO-DATA-BASE
W01968
W01968     IF NOT IO-OK
W01968        DISPLAY '$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$'
W01968        DISPLAY 'BAD LOCK ON EAD: ' W-EAD-050-EMP-ADJUSTMENTS-KEY
W01968        DISPLAY ' DB AREA = ' DB-IO-AREA
W01968        DISPLAY 'ADJUSTMENT NOT PROCESSED FOR: '
W01968                                           W-GBM-053-GROUP-NUMBER
W01968        DISPLAY '$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$'
W01968        GO TO SECTION-EXIT
W01968     END-IF
W01968
W01968     MOVE IO-AREA                    TO W-EAD-EMP-ADJUSTMENTS-DATA
W01968     MOVE ZERO                       TO W-EAD-200-ADJ-BILLED-DATE
W01968     MOVE W-EAD-EMP-ADJUSTMENTS-DATA    TO IO-AREA
W01968     MOVE W-EAD-050-EMP-ADJUSTMENTS-KEY TO IO-KEY
W01968     MOVE IO-WRITE-UPDATE               TO IO-FUNC
W01968
W01968     PERFORM IO-DATA-BASE
W01968
W01968     IF NOT IO-OK
W01968        DISPLAY '$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$'
W01968        DISPLAY 'BAD WRITE ON EAD: ' W-EAD-050-EMP-ADJUSTMENTS-KEY
W01968        DISPLAY ' DB AREA = ' DB-IO-AREA
W01968        DISPLAY 'ADJUSTMENT NOT PROCESSED FOR: '
W01968                                           W-GBM-053-GROUP-NUMBER
W01968        DISPLAY '$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$'
W01968        GO TO SECTION-EXIT
W01968     END-IF
W01968
W01968     GO TO 4000-READ-NEXT-EAD.
W01968     .
W01968 SECTION-EXIT.
W01968     EXIT.
W01968     EJECT
013390
E00619 7000-READ-GBM-PER               SECTION.
E00619
E00619*          GET GBM FOR PREV BATCH DATE/NUM FOR TAGGING PREV LBR'S
013430     MOVE 'GB'                   TO W-GBM-051-RECORD-ID
013440     MOVE '05'                   TO W-GBM-052-GROUP-BLG-TYPE
013450     MOVE RB-PRD-BILLING-FILE-ID TO W-GBM-053-GROUP-NUMBER
013460     MOVE RB-KEY-BILLING-DAY     TO W-GBM-054-PAYMENT-DAY

013480     MOVE W-GBM-050-GROUP-BILLING-KEY TO IO-KEY
013490     MOVE IO-READ-EXACT-INQ      TO IO-FUNC

013510     PERFORM IO-DATA-BASE

013530     IF NOT IO-OK
013540        DISPLAY 'NO GBM REC:' W-GBM-050-GROUP-BILLING-KEY
013550        PERFORM STANDARD-BATCH-ABEND
013560     .
013570     MOVE IO-AREA              TO W-GBM-GROUP-BLG-MASTER-DATA
E00619     .
E00619 7010-GET-GBM-NAME.
E00619*            GET PER FOR GBM NAME
E00619
013620     MOVE 'P '                  TO W-PER-032-RECORD-ID
013630     MOVE 'G'                   TO W-PER-034-ACCOUNT-TYPE
013640     MOVE RB-PRD-BILLING-FILE-ID TO W-PER-036-ACCOUNT-NUMBER
013650     MOVE ZEROS                 TO W-PER-038-RECORD-NUMBER
013660     MOVE W-PER-030-PERSON-MASTER-KEY
013670                                TO IO-KEY
013680     MOVE IO-START-BROWSE-GTEQ  TO IO-FUNC
013690
013700     PERFORM IO-DATA-BASE
013710
013720     IF NOT IO-OK
013730        DISPLAY '7000- BAD STBR ON PER: '
013740              W-PER-030-PERSON-MASTER-KEY
013750        MOVE ' **UNKNOWN**         ' TO DTL-CLIENT-NAME
013760     .
013770
013780 7020-READ-NEXT.
013790
013800     MOVE W-PER-030-PERSON-MASTER-KEY
013810                               TO IO-KEY
013820     MOVE IO-READ-NEXT         TO IO-FUNC
013830
013840     PERFORM IO-DATA-BASE
013850
013860     IF NOT IO-OK
013870        DISPLAY '7000- BAD RDNX ON PER: '
013880               W-PER-030-PERSON-MASTER-KEY
013890        PERFORM STANDARD-BATCH-ABEND
013900     .
013910
013920     MOVE IO-AREA          TO W-PER-PERSON-MASTER-DATA
013930
013940     IF  W-PER-032-RECORD-ID       = 'P '
013950     AND W-PER-034-ACCOUNT-TYPE    = 'G'
013960     AND W-PER-036-ACCOUNT-NUMBER  = W-GBM-053-GROUP-NUMBER
013970         IF  W-PER-160-TYPE        = 'C'
013980         AND W-PER-078-STATUS-CODE = 'A'
013990             MOVE W-PER-090-NAME TO DTL-CLIENT-NAME
014000         ELSE
014010             GO TO 7020-READ-NEXT
014020     .
014030
014040     DISPLAY '7000- CLIENT NAME= ' W-PER-090-NAME.
014050
014060     .

014080 SECTION-EXIT.
014090     EXIT.
014100     EJECT
014110
014120 8000-ERROR-PRINT    SECTION.
014130
E00619     SET  WS-ERRORS           TO TRUE
014150     MOVE DETAIL-LINE         TO O-PRT-DETAIL-LINE
014160     PERFORM O-PRT-WRITE-AFTER-2
014170     .
014180
E00619     MOVE SPACES  TO  DTL-MESSAGES
E00619     .
014210 SECTION-EXIT.
014220     EXIT.
014230     EJECT
014240
014250
014260 COPY IMCPOPRT.
014270 COPY IMCPSTDH.
014280 COPY IMCBPDBI.
014290 COPY IMCBTBL.
014300 COPY IMCBJGCP.
014310 COPY IMCPEDIT.
014320
014330******************************************************************
